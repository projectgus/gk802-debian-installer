# Find /boot/zImage or /zImage and a matching /initrd or /boot/initrd and
# boot them
#
# (or zImage_recovery / initrd_recovery if in recovery mode)
#
# Values assumed to be preset by parent uboot - loadaddr, bootsuffix

setenv initaddr 0x12008000;
# increase this number if your initrd gets bigger than 48Mb(!)
setenv initrdsz 0x03000000;

# (@ signs get for ROOTDEV processed by install_kernel_boot.sh script)
setenv bootargs 'root=@ROOTDEV@ rw';
# Recovery mode boots with interactive (early boot initrd) console on serial port, non-recovery interactive on hdmi output
if test ${recovery} -gt 0; then
   setenv bootargs ${bootargs}' console=tty0 console=ttymxc3,115200';
else
   setenv bootargs ${bootargs}' console=ttymxc3,115200 console=tty0';
fi;

setenv tryboot 'if ext2load ${dtype} ${dev} ${loadaddr} ${path}/zImage${bootsuffix}; then
       if ext2load ${dtype} ${dev} ${initaddr} ${path}/initrd${bootsuffix}; then
           bootz ${loadaddr} ${initaddr}:${initrdsz};
       fi;
   fi;';

# If partition is dedicated boot partition then files will be in root, otherwise in /boot
setenv trypaths 'setenv path /boot; run tryboot; setenv path ''; run tryboot;'

setenv dtype mmc
setenv dev 1;
run trypaths;

setenv dev 0;
run trypaths;

# Switch to USB mass storage. Try up to 5(!) devices to see if we can find one.
usb start
setenv dtype usb
setenv dev 0
run trypaths

setenv dev 1
run trypaths

setenv dev 2
run trypaths

setenv dev 3
run trypaths

setenv dev 4
run trypaths

setenv dev 5
run trypaths
